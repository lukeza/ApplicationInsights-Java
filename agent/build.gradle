/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

configurations.all {
    // this is only needed while using xyzzy SNAPSHOTS
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    // this is only needed while using xyzzy SNAPSHOTS
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

apply from: "$buildScriptsDir/common-java.gradle"


def xyzzyVersion = '0.5.0-SNAPSHOT'

def shadowPrefix = 'com.microsoft.applicationinsights.agent.shadow'
def corePrefix = 'com.microsoft.applicationinsights.core.dependencies' // some dependencies are already shaded in core

shadowJar {
    classifier = ''
    mergeServiceFiles()

    relocate('org.objectweb.asm', "${shadowPrefix}.org.objectweb.asm")
    relocate('org.slf4j', "${shadowPrefix}.org.slf4j")
    // FIXME need to relocate logback also, but currently causes instrumentation to be relocated
    // relocate('ch.qos.logback', "${shadowPrefix}.ch.qos.logback")
    relocate('com.fasterxml.jackson', "${shadowPrefix}.com.fasterxml.jackson")

    // core
    relocate 'com.google', "${corePrefix}.google"
    relocate 'org.checkerframework', "${corePrefix}.checkerframework"
    relocate 'org.apache.commons.logging', "${corePrefix}.apachecommons.logging"

    relocate('logback.configurationFile', 'ai.logback.configurationFile')
    relocate('logback.xml', 'ai.logback.xml')
    relocate('logback.groovy', 'ai.logback.groovy') // to prevent accidentally picking up user's logback.groovy
    relocate('logback-test.xml', 'ai.logback-test.xml') // to prevent accidentally picking up user's logback-test.xml

    exclude 'META-INF/maven/**'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/services/javax.servlet.ServletContainerInitializer'
    exclude 'mozilla/**'
    exclude 'commons-logging.properties'

    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'META-INF/xyzzy.instrumentation.list'
    }
}
archivesBaseName = 'applicationinsights-agent'

jar {
    dependsOn shadowJar
    enabled = false
    manifest {
        attributes("Premain-Class": "com.microsoft.applicationinsights.agent.internal.Premain",
                "Can-Redefine-Classes": "true",
                "Can-Retransform-Classes": "true",
                "agent-sdk-version": project.version
        )
    }
}

dependencies {
    compile project(':ApplicationInsightsInternalLogger') // TODO include? or use slf4j?
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile "org.glowroot.xyzzy:xyzzy-engine:$xyzzyVersion"
    compile "org.glowroot.xyzzy:xyzzy-instrumentation-api:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-executor:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-servlet:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-spring:$xyzzyVersion" // need for operation name
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-jdbc:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-redis:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-apache-http-client:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-http-url-connection:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-okhttp:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-log4j:$xyzzyVersion"
    runtime "org.glowroot.xyzzy.instrumentation:instrumentation-logback:$xyzzyVersion"

    compile(project(path: ':core', configuration: 'shadow'))

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
}
